apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'

ext{
    RXJAVA_VERSION = '2.0.1'
    RETROFIT_RXJAVA_ADAPTER_VERSION = '1.0.0'

    DAGGER_VERSION = '2.9'
    ANNOTATION_JSR250_VERSION = '1.0'

    RETROFIT_VERSION = '2.2.0'
    OKHTTP_LOGGING_INTERCEPTOR_VERSION = '3.6.0'

    FIREBASE_VERSION = '10.2.6'

    ANDROID_SUPPORT_APPCOMPAT_VERSION = '25.3.1'
    ANDROID_CONTRIB_VERSION = '0.3'
    ANDROID_THINGS_VERSION = '0.4-devpreview'
}

android {
    compileSdkVersion 25
    buildToolsVersion "26.0.0"
    defaultConfig {
        applicationId "com.vctapps.bustracker"
        minSdkVersion 24
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        applicationVariants.all { variant ->
            variant.buildConfigField "String", "BASE_URL", "\"" + getBaseUrl() + "\""
        }

    }
}

def getBaseUrl(){
    def Properties props = new Properties()
    props.load(new FileInputStream(new File('secrets.properties')))
    return props['BASE_URL']
}

dependencies {

    compile 'com.android.support:multidex:1.0.1'

    // Android
    compile "com.android.support:appcompat-v7:${ANDROID_SUPPORT_APPCOMPAT_VERSION}"

    // Android Things
    compile "com.google.android.things.contrib:driver-gps:${ANDROID_CONTRIB_VERSION}"
    provided "com.google.android.things:androidthings:${ANDROID_THINGS_VERSION}"

    // RXJAVA
    compile "io.reactivex.rxjava2:rxandroid:${RXJAVA_VERSION}"
    compile "io.reactivex.rxjava2:rxjava:${RXJAVA_VERSION}"
    compile "com.jakewharton.retrofit:retrofit2-rxjava2-adapter:${RETROFIT_RXJAVA_ADAPTER_VERSION}"

    // LOGGING INTERCEPTOR
    compile "com.squareup.okhttp3:logging-interceptor:${OKHTTP_LOGGING_INTERCEPTOR_VERSION}"

    // DAGGER
    compile "com.google.dagger:dagger:${DAGGER_VERSION}"
    kapt "com.google.dagger:dagger-compiler:${DAGGER_VERSION}"

    // RETROFIT
    compile "com.squareup.retrofit2:retrofit:${RETROFIT_VERSION}"
    compile "com.squareup.retrofit2:converter-gson:${RETROFIT_VERSION}"

    //FIREBASE
    compile "com.google.firebase:firebase-database:${FIREBASE_VERSION}"

    // Tests
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testCompile 'junit:junit:4.12'
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
}
repositories {
    mavenCentral()
}

apply plugin: 'com.google.gms.google-services'